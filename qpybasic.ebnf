%ignore /[ \t\f]+/

start: program

program: (line NEWLINE)* line NEWLINE?

line: [label | lineno] stmt_group

stmt_group: (stmt ":")* [stmt ":"?]

label: ID ":"

lineno: INT_LITERAL

stmt: cls_stmt
    | end_stmt
    | goto_stmt
    | let_stmt
    | print_stmt

cls_stmt: CLS_KW

end_stmt: END_KW

goto_stmt: GOTO_KW (ID | INT_LITERAL)

let_stmt: LET_KW? ID "=" expr

print_stmt: PRINT_KW (expr (SEMICOLON | COMMA))* [expr [SEMICOLON | COMMA]]

expr: add_expr
    | expr "<" add_expr   -> expr_lt
    | expr ">" add_expr   -> expr_gt
    | expr "<=" add_expr  -> expr_le
    | expr ">=" add_expr  -> expr_ge
    | expr "=" add_expr   -> expr_eq
    | expr "<>" add_expr   -> expr_ne

add_expr: mult_expr
        | add_expr "+" mult_expr  -> expr_add
        | add_expr "-" mult_expr  -> expr_sub

mult_expr: unary_expr
         | mult_expr "*" unary_expr  -> expr_mult
         | mult_expr "/" unary_expr  -> expr_div

unary_expr: value
          | "-" expr      -> negation
          | "(" expr ")"

value: INT_LITERAL
     | STRING_LITERAL
     | var

var: ID

NEWLINE: "\n"
SEMICOLON: ";"
COMMA: ","
ID: /[a-z_][a-z0-9]*/i
INT_LITERAL: /\d+/
STRING_LITERAL: /"[^"]*"/

CLS_KW: "cls"i
END_KW: "end"i
GOTO_KW: "goto"i
LET_KW: "let"i
PRINT_KW: "print"i
