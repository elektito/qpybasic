%ignore /[ \t\f]+/

start: program

program: ((line | block) NEWLINE)* (line | block) NEWLINE?

block: if_block

line: [label | lineno] stmt_group

stmt_group: (stmt ":")* [stmt ":"?]

label: ID ":"

lineno: INT_LITERAL

stmt: cls_stmt
    | end_stmt
    | goto_stmt
    | let_stmt
    | print_stmt

if_block: IF_KW expr THEN_KW NEWLINE block_body else_list END_KW IF_KW

else_list: ELSEIF_KW expr THEN_KW NEWLINE block_body else_list  -> elseif_sub_block
         | ELSE_KW NEWLINE block_body                           -> else_sub_block
         |

block_body: block_body_item block_body
          |

block_body_item: block NEWLINE
               | line NEWLINE
               | NEWLINE

cls_stmt: CLS_KW

end_stmt: END_KW

goto_stmt: GOTO_KW (ID | INT_LITERAL)

let_stmt: LET_KW? ID "=" expr

print_stmt: PRINT_KW (expr (SEMICOLON | COMMA))* [expr [SEMICOLON | COMMA]]

expr: add_expr
    | expr "<" add_expr   -> expr_lt
    | expr ">" add_expr   -> expr_gt
    | expr "<=" add_expr  -> expr_le
    | expr ">=" add_expr  -> expr_ge
    | expr "=" add_expr   -> expr_eq
    | expr "<>" add_expr   -> expr_ne

add_expr: mult_expr
        | add_expr "+" mult_expr  -> expr_add
        | add_expr "-" mult_expr  -> expr_sub

mult_expr: unary_expr
         | mult_expr "*" unary_expr  -> expr_mult
         | mult_expr "/" unary_expr  -> expr_div

unary_expr: value
          | "-" expr      -> negation
          | "(" expr ")"

value: INT_LITERAL
     | STRING_LITERAL
     | var

var: ID

NEWLINE: "\n"
SEMICOLON: ";"
COMMA: ","
ID: /[a-z_][a-z0-9]*/i
INT_LITERAL: /\d+/
STRING_LITERAL: /"[^"]*"/

CLS_KW: "cls"i
ELSE_KW: "else"i
ELSEIF_KW: "elseif"i
END_KW: "end"i
GOTO_KW: "goto"i
IF_KW: "if"i
LET_KW: "let"i
PRINT_KW: "print"i
THEN_KW: "then"i